buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'de.richsource.gradle.plugins:gwt-gradle-plugin:0.6'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7'
        classpath 'net.researchgate:gradle-release:2.4.0'
    }
}

apply plugin: 'java'
apply plugin: 'gwt-base'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'net.researchgate.release'

group = 'com.nwalter.gwt'
sourceCompatibility = '1.7'
targetCompatibility = '1.7'

ext {
    gwtVersion = '2.5.1'
}

repositories {
	jcenter()
}

dependencies {
	compile 'com.vaadin:vaadin-sass-compiler:0.9.13'
	compile 'com.google.guava:guava:17.0'

	testCompile 'junit:junit:4.12'
}

gwt {
    gwtVersion = project.gwtVersion
}

task sourceJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allJava
}

jar {
    from(sourceSets.main.allSource) {
        include 'com/nwalter/gwt/scss/client/**'
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java

            artifact sourceJar {
                classifier 'sources'
            }
        }
    }
}

bintrayUpload.dependsOn 'generatePomFileForMavenPublication'

bintray {
    user = project.hasProperty('bintray.user') ? project.property('bintray.user') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintray.key') ? project.property('bintray.key') : System.getenv('BINTRAY_KEY')
    publications = ['maven']
    publish = true

    pkg {
        repo = 'maven'
        name = 'gwt-scss-resource'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/TwoStone/gwt-scss-resource.git'
        websiteUrl = 'https://github.com/TwoStone/gwt-scss-resource'
        issueTrackerUrl = 'https://github.com/TwoStone/gwt-scss-resource/issues'
        labels = ['gwt','scss','sass']
        publicDownloadNumbers = true

        githubRepo = 'TwoStone/gwt-scss-resource'

        version {
            name = project.version
            desc = "GWT SCSS Resource Version ${project.version}"
            released = new Date()
            vcsTag = "${project.version}"

            gpg {
                sign = true
            }
        }
    }
}
